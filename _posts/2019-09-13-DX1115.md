---
title: "DirectX 11 学习笔记-15【骨骼动画与蒙皮】"
tags: 图形学
      C++
---

## 模型

M3D是《Introduction to 3D Game Programming with Directx 11》中提供的一种简单格式。一般来说每个引擎中都会根据自己的需求将模型导出成不同的格式，再运用到渲染引擎中。<!--more-->

大部分的3D建模程序都会提供工具，让程序员能够编写插件从而导出自己定义的模型格式。当然还有另一种方法，就是通过一个converter将标准的模型格式转换成特定应用所使用的模型格式。

这里使用的M3D是文本格式文件，方便人阅读和手工编辑它们。实际上，二进制格式是更加节省内存的。

关于M3D的解析相关的逻辑，没有特别多需要注意的地方，所以我们不再详细叙述。不过特别提一点是，这里的模型中有Subset的概念。一个模型可以有一个或者多个Subset，一个Subset可以是一组相同的材质来渲染的一组三角形。相同的材质意味着相同的effect，textures和render states，因此一个Subset可以在一次DrawCall内完成绘制。

## 动画帧的层级机构

许多的物体都是由多个部分组成的，带有一个父子层级关系，一个或多个的子物体可以自己独立运动，但是也会随着它们的父级一同运动。比如下图所示的层级关系

![1](/assets/images/2019-09-13-DX1115/1.jpg)

我们知道互相连接的物体都在同一个宇宙中，他们彼此关联，我们用物体相对于父物体的坐标系来描述动画中的物体。假设Root$F_0$在世界坐标系W中，即$F_0$和世界坐标系相关联。假设$A_0$是从$F_0$坐标系变换到W的变换矩阵，$A_1$是从$F_1$坐标系变换到$F_0$坐标系的变换矩阵，则对$A_1$上的物体连续进行$A_1$和$A_0$变换可以得到$A_1$物体在世界坐标系上的矩阵表示。这条规则对任意深度的子物体都有效，所以有

$$M_i = A_iA_{i-1}···A_1A_0$$

其中$A_i$表示从$F_i$变换到$F_{i-1}$的变换矩阵。

除了一个简单的线性层级关系外，通过相同的方法可以扩展到树状层级结构。也就是说，对任何层级中的object，世界坐标空间上的transformation通过将对父级的变换逐层递归到祖先中，直到变换至世界坐标空间。

## 模型的蒙皮

下图展示了骨骼和模型。其中，高亮的步伐为骨骼（skeleton）。一个骨骼提供了一个天然的层级结构，用于驱动角色的动画系统。

骨骼由skin所包裹，skin是我们用3D几何体（顶点和多边形）构成的model。

首先，skin的顶点是相对于绑定空间（Bind Space）的，它是整个skin所在的一个local的坐标系系统。

每个骨点（Bone）影响了skin的subset的形状和位置。因此我们需要让骨骼动起来，附着在骨骼上的skin是是当前骨骼位置的映射。

![2](/assets/images/2019-09-13-DX1115/2.jpg)

## 将bones转换到root空间的变换

比起直接计算出每个骨点对世界坐标的变换矩阵，先计算出每个骨点对root的变换矩阵，再通过root到世界坐标的变换矩阵求出骨点对应的世界坐标要更加方便。

为了节省计算，我们的递归从root开始向树的叶节点方向递归。用数字为n个骨点编上编号，我们有如下的公式来表示第i个骨点对root的变换

$$toRoot_i=toParent_i\cdot toRoot_p$$

此处p是父骨点的id，$toRoot_p$代表了一个从父节点上的局部坐标到root的一个变换。因此为了变换至root坐标系系统，我们只需要将几何体从骨点i的坐标变换到它的父级骨点p，$toParent_i$可以完成这个工作。

唯一的问题是，当我们处理第i个骨点是，我们必须已经完成了它的父节点的to-root变换的计算。因此如果我们自顶向下地遍历，那么父节点对root的变换总是在子节点计算之前。

## offset的变换

一个细微的地方是，受骨点影响的vertices与骨点的坐标系统是无关的（他们只和建模时的bind space有关）。所以在我们应用等式变换到世界坐标之前，我们需要将顶点从绑定空间变换到骨点的空间。这被叫做offset-transformation。

![3](/assets/images/2019-09-13-DX1115/3.jpg)

因此，通过位移矩阵将顶点从绑定空间移动到了骨点b的空间。然后一旦我们有顶点在骨点b上的位置，我们可以使用b对root的变换来把顶点变换到当前的动画下，它在root空间的位置。

最后我们引入一个新的变换叫做final transform，将bone上顶点的位移变换和它的to-root变换组合起来。

$$F_i=offset_i\cdot toRoot_i$$

## 顶点混合

顶点混合的策略如下。

我们有一个潜在的骨点层级，skin自己是一个连续的mesh（我们不把mesh分成多个部分）。此外，一个或多个骨点可以同时影响skin上的一个vertex。最终的结果通过一个带有比重的平均来获得最终的transform（weights是由艺术家在建模的时候指定的），通过这一步，关节中可以有一个平滑的过渡，从而让关节更加灵活。

在实际上，我们通常不需要超过四个骨点来影响一个顶点，因此在我们的设计中，我们将每个顶点最大影响骨点数设置为4。每个顶点包括最多四个索引，这些索引被叫做bone matrix palette，是最终变换矩阵的索引。此外每个顶点最多有四个weights，用于描述每个骨点对顶点的影响程度。

![4](/assets/images/2019-09-13-DX1115/4.jpg)

对于一个连续的Mesh，如果它的顶点是用于蒙皮的，那么我们可以称之为skinned mesh。

任何顶点v混合后的位置v'，可以通过下面的方式来计算

$$v'=w_0vF_0+w_1vF_1+w_2vF_2+w_3vF_3$$

此处的$w_0+w_1+w_2+w_3=1$。

在这个等式中，我们独立计算v受每一个骨点的影响。之后我们做一个带有比重的平均，从而计算出最终混合后的顶点位置。

normals和tangents也可以简单地模拟为

$$n'=normalize(w_0nF_0+w_1nF_1+w_2nF_2+w_3nF_3)$$
$$t'=normalize(w_0tF_0+w_1tF_1+w_2tF_2+w_3tF_3)$$

这里我们假设变换矩阵不含任何非均匀缩放，否则我们需要使用逆转置矩阵。

## 代码实现

代码已上传至[GitHub](https://github.com/ZealotAlie/D11Demo0)，这部分对应的项目为AnimatedSkinnedMesh。

原书提供的实例代码要更加复杂，我删去了与蒙皮无关的逻辑并简化了着色器。

我们主要看两个部分的代码，首先看final transforms的求解

```c++
void SkinnedData::GetFinalTransforms(const std::string& clipName, float timePos,  std::vector<XMFLOAT4X4>& finalTransforms)const
{
	UINT numBones = mBoneOffsets.size();

	XMFLOAT4X4 toParentTransforms[BONE_MAX_NUM];

	// Interpolate all the bones of this clip at the given time instance.
	auto clip = mAnimations.find(clipName);
	clip->second.Interpolate(timePos, toParentTransforms);

	//
	// Traverse the hierarchy and transform all the bones to the root space.
	//

	XMFLOAT4X4 toRootTransforms[BONE_MAX_NUM];

	// The root bone has index 0.  The root bone has no parent, so its toRootTransform
	// is just its local bone transform.
	toRootTransforms[0] = toParentTransforms[0];

	// Now find the toRootTransform of the children.
	for(UINT i = 1; i < numBones; ++i)
	{
		XMMATRIX toParent = XMLoadFloat4x4(&toParentTransforms[i]);

		int parentIndex = mBoneHierarchy[i];
		XMMATRIX parentToRoot = XMLoadFloat4x4(&toRootTransforms[parentIndex]);

		XMMATRIX toRoot = XMMatrixMultiply(toParent, parentToRoot);

		XMStoreFloat4x4(&toRootTransforms[i], toRoot);
	}

	// Premultiply by the bone offset transform to get the final transform.
	for(UINT i = 0; i < numBones; ++i)
	{
		XMMATRIX offset = XMLoadFloat4x4(&mBoneOffsets[i]);
		XMMATRIX toRoot = XMLoadFloat4x4(&toRootTransforms[i]);
		XMStoreFloat4x4(&finalTransforms[i], XMMatrixMultiply(offset, toRoot));
	}
}
```

首先我们根据时间信息来进行插值，从而求出每个骨点对父骨点的变换矩阵，即代码中的toParentTransforms。然后我们需要求出所有骨点的toRootTransforms。

这里按照mBoneHierarchy的顺序来遍历全部的骨点，所有这里的要求第i个骨点的parent的index必须要小于i，否则我们就不能直接得到正确的toRootTransforms[parentIndex]的值了。

然后我们将mBoneOffsets和toRootTransforms相乘，即可得到finalTransforms。

另外一个需要注意的是Vertex Shader。首先我们必须把模型的finalTransform传到GPU中，因此在着色器代码中必须声明一个矩阵数组

```glsl
cbuffer cbSkinned
{
	float4x4 gBoneTransforms[96];
};

VertexOut SkinnedVS(SkinnedVertexIn vin)
{
    VertexOut vout;

	// Init array or else we get strange warnings about SV_POSITION.
	float weights[4] = {0.0f, 0.0f, 0.0f, 0.0f};
	weights[0] = vin.Weights.x;
	weights[1] = vin.Weights.y;
	weights[2] = vin.Weights.z;
	weights[3] = 1.0f - weights[0] - weights[1] - weights[2];

	float3 posL     = float3(0.0f, 0.0f, 0.0f);
	float3 normalL  = float3(0.0f, 0.0f, 0.0f);
	float3 tangentL = float3(0.0f, 0.0f, 0.0f);
	for(int i = 0; i < 4; ++i)
	{
	    // Assume no nonuniform scaling when transforming normals, so 
		// that we do not have to use the inverse-transpose.

	    posL     += weights[i]*mul(float4(vin.PosL, 1.0f), gBoneTransforms[vin.BoneIndices[i]]).xyz;
		normalL  += weights[i]*mul(vin.NormalL,  (float3x3)gBoneTransforms[vin.BoneIndices[i]]);
		tangentL += weights[i]*mul(vin.TangentL.xyz, (float3x3)gBoneTransforms[vin.BoneIndices[i]]);
	}
 
	// Transform to world space space.
	vout.PosW     = mul(float4(posL, 1.0f), gWorld).xyz;
	vout.NormalW  = mul(normalL, (float3x3)gWorldInvTranspose);
	vout.TangentW = float4(mul(tangentL, (float3x3)gWorld), vin.TangentL.w);

	// Transform to homogeneous clip space.
	vout.PosH = mul(float4(posL, 1.0f), gWorldViewProj);
	
	// Output vertex attributes for interpolation across triangle.
	vout.Tex = mul(float4(vin.Tex, 0.0f, 1.0f), gTexTransform).xy;

	return vout;
}

```

然后根据传入的gBoneTransforms来混合顶点位置和法线等信息即可。剩余的流程与之前的绘制无异。

最终可以看到运动的士兵模型

![5](/assets/images/2019-09-13-DX1115/5.jpg)

## 参考资料

- 《Introduction to 3D Game Programming with Directx 11》