---
title: "DirectX 11 学习笔记-13【Cubemap/贴图置换/阴影/SSAO】【实现】"
tags: 图形学
      C++
---

代码已上传至[GitHub](https://github.com/ZealotAlie/D11Demo0)，这部分对应的项目为NormalDisplacementSsao。运行项目时可以使用数字键1-3切换渲染的shader，另外按下z键可以看到SSAO生成的遮罩贴图。<!--more-->

这篇文章主要结合代码介绍渲染的流程（对应代码中的NDSsaoApp::DrawScene）。

## Shadow Map

首先我们要渲染阴影深度图，看ShadowMap的定义

```c++
class ShadowMap
{
    ... ...
	ID3D11ShaderResourceView* mDepthMapSRV;
	ID3D11DepthStencilView*   mDepthMapDSV;

	D3D11_VIEWPORT mViewport;
};
```

因为生成的深度图，既要作为管线的输出，也要作为着色器的输入，所以创建的2D纹理需要同时有ID3D11ShaderResourceView和ID3D11DepthStencilView。另外ShadowMap中定义了自己的Viewport，可以渲染出自己需要的大小的纹理。

纹理创建的过程和一般的模板深度纹理相同，但是要注意BindFlags要同时包D3D11_BIND_DEPTH_STENCIL和D3D11_BIND_SHADER_RESOURCE。

绘制阴影深度图，第一步是要把ShadowMap中的DepthStencilView绑定到管线的输出阶段。

```c++
void ShadowMap::BindDsvAndSetNullRenderTarget(ID3D11DeviceContext* dc)
{
    dc->RSSetViewports(1, &mViewport);

	// Set null render target because we are only going to draw to depth buffer.
	// Setting a null render target will disable color writes.
    ID3D11RenderTargetView* renderTargets[1] = {0};
    dc->OMSetRenderTargets(1, renderTargets, mDepthMapDSV);
    
    dc->ClearDepthStencilView(mDepthMapDSV, D3D11_CLEAR_DEPTH, 1.0f, 0);
}
```

这里在设置RenderTarget时，使用了空的RenderTargetView，因为我们只需要绘制深度buffer不需要输出颜色。

设置好输出后，我们要向DepthStencilView中绘制深度信息。

前面说过，我们有三种绘制形式，普通绘制，NormalMap绘制和贴图置换绘制。前两种对生成的阴影形状没有影响，因此在这里可以归为一种，贴图置换由于会影响物体的表面，所以在绘制阴影时也要做贴图置换。

```c++
void NDSsaoApp::DrawSceneToShadowMap()
{
        //Setup Const buffer... ...

	if(mDrawOptions != RenderOptionsDisplacementMap)
	{
		md3dImmediateContext->IASetPrimitiveTopology(D3D11_PRIMITIVE_TOPOLOGY_TRIANGLELIST);
		for(size_t i =0;i<mSceneObjects.size();++i)
		{
			mSceneObjects[i]->Draw(this,BuildShadowMapEffect::Ptr(),BuildShadowMapEffect::Ptr()->BuildShadowMapTech, 
				&mLightView, &mLightProj);
		}
	}
	else
	{
		md3dImmediateContext->IASetPrimitiveTopology(D3D11_PRIMITIVE_TOPOLOGY_3_CONTROL_POINT_PATCHLIST);
		for(size_t i =0;i<mSceneObjects.size();++i)
		{
			mSceneObjects[i]->Draw(this,BuildShadowMapEffect::Ptr(),BuildShadowMapEffect::Ptr()->TessBuildShadowMapTech, 
				&mLightView, &mLightProj);
		}
	}


	// FX sets tessellation stages, but it does not disable them.  So do that here
	// to turn off tessellation.
	md3dImmediateContext->HSSetShader(0, 0, 0);
	md3dImmediateContext->DSSetShader(0, 0, 0);

	md3dImmediateContext->IASetPrimitiveTopology(D3D11_PRIMITIVE_TOPOLOGY_TRIANGLELIST);


	mSkullVertexState.Apply(md3dImmediateContext);
	mSkull.Draw(this,BuildShadowMapEffect::Ptr(),BuildShadowMapEffect::Ptr()->BuildShadowMapTech, &mLightView, &mLightProj);
}
```

绘制前要设置许多的变量，这部分请查看源码。我们先看不做贴图置换的情况，此时使用的是BuildShadowMapTech，对应到着色器代码中

```glsl
RasterizerState Depth
{
	DepthBias = 100000;
    DepthBiasClamp = 0.0f;
	SlopeScaledDepthBias = 1.0f;
};
VertexOut VS(VertexIn vin)
{
	VertexOut vout;

	vout.PosH = mul(float4(vin.PosL, 1.0f), gWorldViewProj);
	vout.Tex  = mul(float4(vin.Tex, 0.0f, 1.0f), gTexTransform).xy;

	return vout;
}
technique11 BuildShadowMapTech
{
    pass P0
    {
        SetVertexShader( CompileShader( vs_5_0, VS() ) );
        SetGeometryShader( NULL );
        SetPixelShader( NULL );
        SetRasterizerState(Depth);
    }
}
```

我们不需要输出颜色只需要深度信息，因此我们只需要VertexShader。在Vertex Shader的输出中，SV_POSITION是必须的，因为光栅化阶段的裁剪还是会进行的，而且我们需要由它来计算深度信息，而TEXCOORD是不需要的，但是如果输出中只有一个SV_POSITION在编译shader时会报错。

贴图置换时的绘制过程同样也是不需要输出颜色的，因此可以不需要PS。

```glsl
technique11 TessBuildShadowMapTech
{
    pass P0
    {
        SetVertexShader( CompileShader( vs_5_0, TessVS() ) );
		SetHullShader( CompileShader( hs_5_0, HS() ) );
        SetDomainShader( CompileShader( ds_5_0, DS() ) );
        SetGeometryShader( NULL );
        SetPixelShader( NULL );

		SetRasterizerState(Depth);
    }
}
```

这里的曲面细分与之前地形计算略有不同，它把细分参数的计算直接放到了Vertex Shader中进行，在PatchHS中直接取两点的细分参数平均值作为线的细分参数。

```c++
struct TessVertexOut
{
	float3 PosW       : POSITION;
	float3 NormalW    : NORMAL;
	float2 Tex        : TEXCOORD;
	float  TessFactor : TESS;
};

TessVertexOut TessVS(VertexIn vin)
{
	TessVertexOut vout;

	vout.PosW     = mul(float4(vin.PosL, 1.0f), gWorld).xyz;
	vout.NormalW  = mul(vin.NormalL, (float3x3)gWorldInvTranspose);
	vout.Tex      = mul(float4(vin.Tex, 0.0f, 1.0f), gTexTransform).xy;

	float d = distance(vout.PosW, gEyePosW);

	// Normalized tessellation factor. 
	// The tessellation is 
	//   0 if d >= gMinTessDistance and
	//   1 if d <= gMaxTessDistance.  
	float tess = saturate( (gMinTessDistance - d) / (gMinTessDistance - gMaxTessDistance) );
	
	// Rescale [0,1] --> [gMinTessFactor, gMaxTessFactor].
	vout.TessFactor = gMinTessFactor + tess*(gMaxTessFactor-gMinTessFactor);

	return vout;
}

struct PatchTess
{
	float EdgeTess[3] : SV_TessFactor;
	float InsideTess  : SV_InsideTessFactor;
};

PatchTess PatchHS(InputPatch<TessVertexOut,3> patch, 
                  uint patchID : SV_PrimitiveID)
{
	PatchTess pt;
	
	// Average tess factors along edges, and pick an edge tess factor for 
	// the interior tessellation.  It is important to do the tess factor
	// calculation based on the edge properties so that edges shared by 
	// more than one triangle will have the same tessellation factor.  
	// Otherwise, gaps can appear.
	pt.EdgeTess[0] = 0.5f*(patch[1].TessFactor + patch[2].TessFactor);
	pt.EdgeTess[1] = 0.5f*(patch[2].TessFactor + patch[0].TessFactor);
	pt.EdgeTess[2] = 0.5f*(patch[0].TessFactor + patch[1].TessFactor);
	pt.InsideTess  = pt.EdgeTess[0];
	
	return pt;
}
```

输出点的Pos/Normal/Tex坐标直接根据Domain的uv信息，对控制点进行插值得到。由于法线要求是单位向量，所以我们需要对插值之后的法线做normalize。

接着进行贴图置换，首先根据距离手动计算出MipLevel，然后对法线贴图进行采样，得到的alpha值即为该点的法线方向偏移值。

```
dout.PosW += (gHeightScale*(h-1.0))*dout.NormalW;
```

即可求出最终的世界位置坐标。通过世界坐标求出齐次裁剪坐标，输出结构即可。

至此我们获得了阴影深度贴图，在接下来的绘制中会使用到它。

使用曲面细分后，请务必在结束时禁用曲面细分，否则会导致后面的绘制出错。

```c++
// FX sets tessellation stages, but it does not disable them.  So do that here
// to turn off tessellation.
md3dImmediateContext->HSSetShader(0, 0, 0);
md3dImmediateContext->DSSetShader(0, 0, 0);
```

## 屏幕空间环境光遮蔽

### 绘制法线深度贴图

由于绘制阴影深度贴图时我们改变了RenderTarget，所以第一步是要恢复RenderTarget和Viewport。

清空深度模板视图并重新设置Viewport

```c++
md3dImmediateContext->ClearDepthStencilView(mDepthStencilView,D3D11_CLEAR_DEPTH|D3D11_CLEAR_STENCIL, 1.0f, 0);
md3dImmediateContext->RSSetViewports(1, &mScreenViewport);
```

同样的因为我们要输出深度法线贴图，所以我们要设置新的RenderTarget

```c++
ID3D11RenderTargetView* renderTargets[1] = {mNormalDepthRTV};
pContext->OMSetRenderTargets(1, renderTargets, dsv);

// Clear view space normal to (0,0,-1) and clear depth to be very far away.  
float clearColor[] = {0.0f, 0.0f, -1.0f, 1e5f};
pContext->ClearRenderTargetView(mNormalDepthRTV, clearColor);
```

然后我们可以绘制出我们所需要的深度法线贴图

```c++
void NDSsaoApp::DrawSceneToSsaoNormalDepthMap()
{
	md3dImmediateContext->IASetPrimitiveTopology(D3D11_PRIMITIVE_TOPOLOGY_TRIANGLELIST);

	mShapesVertexState.Apply(md3dImmediateContext);
	for(size_t i =0;i<mSceneObjects.size();++i)
	{
		mSceneObjects[i]->Draw(this,SsaoNormalDepthEffect::Ptr(),SsaoNormalDepthEffect::Ptr()->NormalDepthTech);
	}

	md3dImmediateContext->RSSetState(0);

	mSkullVertexState.Apply(md3dImmediateContext);
	mSkull.Draw(this,SsaoNormalDepthEffect::Ptr(),SsaoNormalDepthEffect::Ptr()->NormalDepthTech);
}
```

同样的，由于我封装了着色器常量的设置过程，所以代码看起来比较简单。再看着色器代码，

```glsl
VertexOut VS(VertexIn vin)
{
	VertexOut vout;
	
	// Transform to view space.
	vout.PosV    = mul(float4(vin.PosL, 1.0f), gWorldView).xyz;
	vout.NormalV = mul(vin.NormalL, (float3x3)gWorldInvTransposeView);
		
	// Transform to homogeneous clip space.
	vout.PosH = mul(float4(vin.PosL, 1.0f), gWorldViewProj);
	
	// Output vertex attributes for interpolation across triangle.
	vout.Tex = mul(float4(vin.Tex, 0.0f, 1.0f), gTexTransform).xy;
 
	return vout;
}

float4 PS(VertexOut pin, uniform bool gAlphaClip) : SV_Target
{
	// Interpolating normal can unnormalize it, so normalize it.
    pin.NormalV = normalize(pin.NormalV);

	if(gAlphaClip)
	{
		float4 texColor = gDiffuseMap.Sample( samLinear, pin.Tex );
		 
		clip(texColor.a - 0.1f);
	}
	
	return float4(pin.NormalV, pin.PosV.z);
}
```

因为之后的运算都是在观察坐标系下进行的，所以这里需要摄像头观察坐标系下的位置信息PosV和法线NormalV。这里位置和法线使用的不是同一变换矩阵，对法线，我们要使用点变换矩阵的逆转置矩阵，否则可能出现法线计算不正确的情况。

之后在像素着色器中，直接把法线和位置的z坐标作为颜色输出。由于有深度测试，最终输出的值肯定是可见的位置的法线值。

至此我们的法线深度贴图便渲染完成。

### 计算遮罩贴图

这部分的代码我们直接看着色器代码

```glsl
struct VertexIn
{
	float3 PosL            : POSITION;
	float3 ToFarPlaneIndex : NORMAL;
	float2 Tex             : TEXCOORD;
};
struct VertexOut
{
    float4 PosH       : SV_POSITION;
    float3 ToFarPlane : TEXCOORD0;
	float2 Tex        : TEXCOORD1;
};
VertexOut VS(VertexIn vin)
{
	VertexOut vout;
	
	// Already in NDC space.
	vout.PosH = float4(vin.PosL, 1.0f);

	// We store the index to the frustum corner in the normal x-coord slot.
	vout.ToFarPlane = gFrustumCorners[vin.ToFarPlaneIndex.x].xyz;

	// Pass onto pixel shader.
	vout.Tex = vin.Tex;
	
    return vout;
}
```

首先，我们在创建屏幕四边形时，传入的顶点直接就是NDC空间下的屏幕四角的顶点，所以不需要再计算齐次裁剪坐标。

ToFarPlane是摄像头位置到远平面的一个向量，在渲染前我们预先计算好远平面四个顶点的坐标，然后再传入顶点的索引值。这样做的好处在前一篇提过，因为摄像头的参数可能变化，所以远平面的四个顶点坐标也会变化，比起改变顶点缓冲区中的值，改变着色器常量会容易得多。

像素着色器的计算部分原理篇已经讲得很清楚了，和代码一一对照即可。

### 模糊遮罩贴图

模糊算法和之前的Compute Shader模糊算法相同，不过这里把模糊的计算放到了像素着色器中进行。

```c++
void Ssao::BlurAmbientMap(int blurCount)
{
	for(int i = 0; i < blurCount; ++i)
	{
		// Ping-pong the two ambient map textures as we apply
		// horizontal and vertical blur passes.
		BlurAmbientMap(mAmbientSRV0, mAmbientRTV1, true);
		BlurAmbientMap(mAmbientSRV1, mAmbientRTV0, false);
	}
}
```

事先我们创建好两个纹理，其中一个贴图中存放遮罩贴图作为输入，另一个空白的贴图作为输出，然后进行一次绘制完成水平方向上的模糊，此时不再需要原来的遮罩贴图，我们将它作为输出，水平模糊后的贴图作为输入，在垂直方向上再次进行模糊，即可完成一次模糊操作。

```c++
D3DX11_TECHNIQUE_DESC techDesc;
tech->GetDesc( &techDesc );
for(UINT p = 0; p < techDesc.Passes; ++p)
{
	tech->GetPassByIndex(p)->Apply(0, pContext);
	pContext->DrawIndexed(6, 0, 0);

	// Unbind the input SRV as it is going to be an output in the next blur.
	SsaoBlurEffect::Ptr()->SetInputImage(0);
	tech->GetPassByIndex(p)->Apply(0, pContext);
}
```

唯一需要注意的点是，绘制完成后要把将要作为输出的纹理，从管线的输入解绑，因为同一个缓冲区不能同时作为输入和输出。

## 场景绘制

至此我们完成了场景绘制的准备工作，获得了一份Shadow Map，一份SSAO遮罩贴图，另外我们的天空盒还为我们提供了一份环境贴图。接下来的工作就是使用它们来渲染我们的场景。

这部分我们直接看着色器代码（Basic.fx）。

```glsl
VertexOut VS(VertexIn vin)
{
	VertexOut vout;
	
	// Transform to world space space.
	vout.PosW    = mul(float4(vin.PosL, 1.0f), gWorld).xyz;
	vout.NormalW = mul(vin.NormalL, (float3x3)gWorldInvTranspose);
		
	// Transform to homogeneous clip space.
	vout.PosH = mul(float4(vin.PosL, 1.0f), gWorldViewProj);
	
	// Output vertex attributes for interpolation across triangle.
	vout.Tex = mul(float4(vin.Tex, 0.0f, 1.0f), gTexTransform).xy;

	// Generate projective tex-coords to project shadow map onto scene.
	vout.ShadowPosH = mul(float4(vin.PosL, 1.0f), gShadowTransform);

	// Generate projective tex-coords to project SSAO map onto scene.
	vout.SsaoPosH = mul(float4(vin.PosL, 1.0f), gWorldViewProjTex);

	return vout;
}
```

首先第一步是顶点着色器，世界坐标、齐次裁剪坐标、世界空间的法线坐标和纹理坐标我们之前讨论过。这里还额外计算了ShadowPosH和SsaoPosH。

这里跳过曲面细分和贴图置换的部分，这部分代码比较简单。

### ShadowMap

先考虑阴影。我们的Shadow的信息已经渲染成纹理的形式并存放在GPU之中，我们知道有光才会有阴影，所以阴影带来的影响是某个像素上的光照量减少，而只有被遮挡的位置才会产生阴影。在阴影贴图中，我们存放了距离光源最近的物体的位置信息，因此我们只需要在绘制时，把正在绘制的物体的位置与距离光源最近的物体的位置做一个对比，如果绘制的物体是距离光源最近的物体，那么它是能受到光照的，我们不需要考虑阴影，反之，这个物体处在阴影之中，我们计算光照的时候就要把阴影的影响计算在内。

如何对比两个物体的位置？首先我们必须保证它们都处在同一坐标系中。

![1](/assets/images/2019-01-27-DX1113/1.jpg)

```c++
void NDSsaoApp::BuildShadowTransform()
{
	// Only the first "main" light casts a shadow.
	XMVECTOR lightDir = XMLoadFloat3(&mEnvironment.dirLights[0].Direction);
	XMVECTOR lightPos = -2.0f*mSceneBounds.Radius*lightDir;
	XMVECTOR targetPos = XMLoadFloat3(&mSceneBounds.Center);
	XMVECTOR up = XMVectorSet(0.0f, 1.0f, 0.0f, 0.0f);

	XMMATRIX V = XMMatrixLookAtLH(lightPos, targetPos, up);

	// Transform bounding sphere to light space.
	XMFLOAT3 sphereCenterLS;
	XMStoreFloat3(&sphereCenterLS, XMVector3TransformCoord(targetPos, V));

	// Ortho frustum in light space encloses scene.
	float l = sphereCenterLS.x - mSceneBounds.Radius;
	float b = sphereCenterLS.y - mSceneBounds.Radius;
	float n = sphereCenterLS.z - mSceneBounds.Radius;
	float r = sphereCenterLS.x + mSceneBounds.Radius;
	float t = sphereCenterLS.y + mSceneBounds.Radius;
	float f = sphereCenterLS.z + mSceneBounds.Radius;
	XMMATRIX P = XMMatrixOrthographicOffCenterLH(l, r, b, t, n, f);

	// Transform NDC space [-1,+1]^2 to texture space [0,1]^2
	XMMATRIX T(
		0.5f, 0.0f, 0.0f, 0.0f,
		0.0f, -0.5f, 0.0f, 0.0f,
		0.0f, 0.0f, 1.0f, 0.0f,
		0.5f, 0.5f, 0.0f, 1.0f);

	XMMATRIX S = V*P*T;

	XMStoreFloat4x4(&mLightView, V);
	XMStoreFloat4x4(&mLightProj, P);
	XMStoreFloat4x4(&mShadowTransform, S);
}
```
其中 mLightView 和 mLightProj 是绘制阴影贴图是所使用的正交投影变换矩阵，通过这两个矩阵变换之后，局部坐标就能变换成正交投影平面上的点。由于阴影最终被存放在贴图中，我们还需要把投影后的坐标变换成纹理坐标。变换之后的坐标是位于NDC（-1<=x<=1,-1<=y<=1）坐标的，我们需要把它映射到[0，1]范围内，因此还需要乘上一个T变换矩阵。至此我们得到了局部空间到阴影贴图UV的变换矩阵。

```glsl
float4 PS(VertexOut pin, 
          uniform int gLightCount, 
		  uniform bool gUseTexure, 
		  uniform bool gAlphaClip, 
		  uniform bool gFogEnabled, 
		  uniform bool gReflectionEnabled) : SV_Target
{
... ...
		// Only the first light casts a shadow.
		float3 shadow = float3(1.0f, 1.0f, 1.0f);
		shadow[0] = CalcShadowFactor(samShadow, gShadowMap, pin.ShadowPosH);
... ...
		// Sum the light contribution from each light source.  
		[unroll]
		for(int i = 0; i < gLightCount; ++i)
		{
			float4 A, D, S;
			ComputeDirectionalLight(gMaterial, gDirLights[i], pin.NormalW, toEye, 
				A, D, S);

			ambient += ambientAccess*A;    
			diffuse += shadow[i]*D;
			spec    += shadow[i]*S;
		}
... ...
    return litColor;
}
```

我们只为主要光照绘制阴影，所以只有第一直射光才考虑阴影的问题。函数CalcShadowFactor会计算出阴影带来的影响，如果没有阴影函数返回1，高光和漫反射都和正常计算，如果在阴影之中，则会返回一个小于1的值，高光和漫反射变弱，看起来就是阴影的效果。

```glsl
SamplerComparisonState samShadow
{
	Filter   = COMPARISON_MIN_MAG_LINEAR_MIP_POINT;
	AddressU = BORDER;
	AddressV = BORDER;
	AddressW = BORDER;
	BorderColor = float4(0.0f, 0.0f, 0.0f, 0.0f);

    ComparisonFunc = LESS;
};

static const float SMAP_SIZE = 2048.0f;
static const float SMAP_DX = 1.0f / SMAP_SIZE;

float CalcShadowFactor(SamplerComparisonState samShadow, 
                       Texture2D shadowMap, 
					   float4 shadowPosH)
{
	// Complete projection by doing division by w.
	shadowPosH.xyz /= shadowPosH.w;
	
	// Depth in NDC space.
	float depth = shadowPosH.z;

	// Texel size.
	const float dx = SMAP_DX;

	float percentLit = 0.0f;
	const float2 offsets[9] = 
	{
		float2(-dx,  -dx), float2(0.0f,  -dx), float2(dx,  -dx),
		float2(-dx, 0.0f), float2(0.0f, 0.0f), float2(dx, 0.0f),
		float2(-dx,  +dx), float2(0.0f,  +dx), float2(dx,  +dx)
	};

	[unroll]
	for(int i = 0; i < 9; ++i)
	{
		percentLit += shadowMap.SampleCmpLevelZero(samShadow, 
			shadowPosH.xy + offsets[i], depth).r;
	}

	return percentLit /= 9.0f;
}
```

这里共取9个像素的阴影深度值，和当前位置的深度值做对比。这里使用了函数SampleCmpLevelZero，它对任何取到的纹理元素的值，将纹理值和输入的对比值（depth）进行比较（比较的函数定义在SamplerComparisonState中，这里是Less），如果通过则返回1，否则返回0。随后SampleCmp会按照一般的纹理采样，进行filtering等操作，返回一个[0,1]之间的值。

因此，此处的代码会比较深度贴图中的深度值和我们的点的深度值，如果depth小于纹理中的r的值（阴影深度值）则返回1，此时说明该点不被阴影遮挡，反正返回0，该点存在阴影。

得到阴影强度之后，和光线进行混合就可以得到阴影效果。

### SSAO

SSAO效果的绘制更加简单。

```glsl
float ambientAccess = gSsaoMap.SampleLevel(samLinear, pin.SsaoPosH.xy, 0.0f).r;

// Sum the light contribution from each light source.  
[unroll]
for(int i = 0; i < gLightCount; ++i)
{
	float4 A, D, S;
	ComputeDirectionalLight(gMaterial, gDirLights[i], pin.NormalW, toEye, A, D, S);

	ambient += ambientAccess*A;    
	diffuse += shadow[i]*D;
	spec    += shadow[i]*S;
}
```

由于SSAO只针对环境光，所以这里也只应用到环境光分量A中。采样同样需要一个UV坐标，用矩阵将本地坐标变换到纹理坐标，只需要在NDC坐标的基础上，再将[-1,1]映射到[0,1]空间即可，和阴影贴图的采样相同。采样后获得AmbientAccess值，和环境光分量混合即可得到屏幕空间遮罩的效果。